" ==============
" Initialization
" ==============

" Disable Vi-compatible mode, i.e. make Vim behave in a more useful way.
set nocompatible

" Skip initialization for `vim.tiny` or `vim.small` which might not support used features.
if !has("eval") | finish | endif

scriptencoding utf-8

" Path is hard-coded because Neovim uses the same configuration.
let $VIM_CONFIG_PATH= expand('$HOME') .. '/.vim/vimrc'

" =========
" Functions
" =========

" Strip trailing white spaces
" ===========================

function <SID>StripTrailingWhitespaces() abort
    let l:l = line(".")
    let l:c = col(".")
    keepp %s/\s\+$//e
    call cursor(l, c)
endfunction

" Diff the current buffer with a buffer ID entered on a prompt
" ============================================================

function <SID>DiffCurrentWithBufferById() abort
    let l:buffer_id = input('Diff the current buffer with buffer ID: ')
    redraw " Clears the command line.

    if buffer_id == '' | echo '' | return | endif

    if buffer_id !~ '^\d\+$'
        echohl ErrorMsg
        echom 'Invalid buffer ID: ' .. buffer_id
        echohl None
        return
    endif

    let buffer_id = str2nr(buffer_id)

    if !bufexists(buffer_id)
        echohl ErrorMsg
        echom 'Buffer ID does not exist: ' .. buffer_id
        echohl None
        return
    elseif buffer_id == bufnr('%')
        echohl ErrorMsg
        echom 'Trying to compare the same buffer ID: ' .. buffer_id
        echohl None
        return
    endif

    execute 'DiffBuffById ' .. buffer_id
endfunction

" Delete hidden saved buffers
" ===========================

function <SID>DeleteHiddenSavedBuffers() abort
    let l:tpbl = []
    let l:deleted_count = 0
    call map(range(1, tabpagenr('$')), 'extend(tpbl, tabpagebuflist(v:val))')
    for l:buf in filter(range(1, bufnr('$')), 'bufexists(v:val) && index(tpbl, v:val) == -1')
        if getbufvar(buf, '&modified') == 0
            silent execute 'bwipeout' buf
            let deleted_count += 1
        endif
    endfor
    echo 'Deleted "' .. deleted_count .. '" hidden buffers which are saved.'
endfunction

" Status Line
" ===========

function! SetStatusLine() abort
    let l:statusLine = ''
    let l:statusLine .= '%F'
    let l:statusLine .= '%='
    let l:statusLine .= ' %m'
    let l:statusLine .= '%r'
    let l:statusLine .= '%h'
    let l:statusLine .= '%w'
    let l:statusLine .= ' %y'
    let l:statusLine .= ' %{&spelllang}=%{&spell}'
    let l:statusLine .= ' %{&fileformat}'
    let l:statusLine .= ' %{&fileencoding}'
    let l:statusLine .= ' | %b = %B'
    let l:statusLine .= ' | %l/%L:%c'
    let l:statusLine .= ' | %P'

    if mode() == 'i'
        return '%1*'.l:statusLine
    elseif mode() == 'v' || mode() == "\<C-V>"
        return '%2*'.l:statusLine
    elseif mode() == 'R' || mode() == 'Rv'
        return '%3*'.l:statusLine
    endif
    return '%0*'.l:statusLine
endfunction

" Tab Line
" ========

function! SetTabLine() abort
    let l:tabLine  = ''
    let l:columnsPerTabLabel = (&columns - tabpagenr('$') * 5) / tabpagenr('$')
    let l:tabLabelSize = l:columnsPerTabLabel > 15 ? l:columnsPerTabLabel : 15

    for i in range(tabpagenr('$'))
        " Select the highlighting.
        if i + 1 == tabpagenr()
            let l:tabLine .= '%#TabLineSel#'
        else
            let l:tabLine .= '%#TabLine#'
        endif

        " Set the tab page number (for mouse clicks).
        let l:tabLine .= '%' .. (i + 1) .. 'T'
        " Display tab number (e.g. for use with `<TAB_NUMBER>gt`).
        let l:tabLine .= ' ' .. (i + 1) .. '/'

        " Rest of the tab label is made by `SetTabLabel()` function.
        let l:tabLine .= ' %{SetTabLabel(' .. (i + 1) .. ',' .. l:tabLabelSize .. ')} '
    endfor

    " After the last tab, fill using `TabLineFill` highlight group and reset tab page number.
    let l:tabLine .= '%#TabLineFill#%T'

    " Right-align the label to close the current tab page.
    if tabpagenr('$') > 1
        let l:tabLine .= '%=%#TabLine#%999XX'
    endif

    return l:tabLine
endfunction

function! SetTabLabel(n, tabLabelSize)
    let l:tabLabel = ''
    let l:tabLabelIndicators = ''
    let l:buffer_list = tabpagebuflist(a:n)

    " Add count of windows (panes) in the tab.
    let l:window_count = tabpagewinnr(a:n, '$')
    let l:tabLabelIndicators .= l:window_count > 1 ? l:window_count : ''

    " Add indicator if one of the buffers in the tab page is modified.
    for buffer_number in l:buffer_list
        if getbufvar(buffer_number, "&modified")
            let l:tabLabelIndicators .= '+'
            break
        endif
    endfor

    let  l:tabLabel .= len(l:tabLabelIndicators) > 0 ? l:tabLabelIndicators .. ' ' : ''

    " Add file name.
    let l:window_number = tabpagewinnr(a:n)
    let l:file_name = fnamemodify(bufname(l:buffer_list[l:window_number - 1]), ':t')
    let l:tabLabel .= (empty(l:file_name) ? '[No Name]' : l:file_name)

    if len(l:tabLabel) > a:tabLabelSize
        let l:tabLabel = strpart(l:tabLabel, 0, 8)
            \ .. '~'
            \ .. strpart(l:tabLabel, len(l:tabLabel) - a:tabLabelSize + 10)
    endif

    return l:tabLabel
endfunction

" Swap file
" =========

function <SID>SwapFileHandle() abort
    " Don't do anything on Windows.
    if !has('unix')
        return
    endif

    " Check if any process has the swap file open.
    let l:lsof_result = systemlist('lsof ' .. shellescape(v:swapname) .. ' 2>/dev/null')

    if empty(lsof_result)
        let v:swapchoice = 'r' " recover; swap file isn't in use by another Vim instance.
    else
        let v:swapchoice = 'e'
        let g:swap_file_message = 'This file is also opened for editing in'
            \ .. ' another Vim instance.'
    endif
endfunction

function <SID>SwapFileMessage() abort
    if empty(g:swap_file_message)
        return
    endif

    echohl MoreMsg
    echom g:swap_file_message
    echohl None
    let g:swap_file_message = ''
endfunction

" Yank
" ====

function <SID>YankAllLinesContainingMatchOfLastUsedSearchPattern() abort
    " Clear register `a`.
    let @a = ''

    " Append lines containing match of last used search pattern to register `A`.
    execute 'g##y A'

    " Set the system clipboard to the contents of register `a`.
    let @+ = @a
endfunction

function <SID>YankMatchesOfLastUsedSearchPattern() abort
    if empty(@/)
        echohl ErrorMsg
        echo "No search pattern set."
        echohl None

        return
    endif

    let l:matches = []

    " Iterate over the lines in the entire file.
    for l:line in range(1, line('$'))
        let l:txt = getline(l:line)
        let l:start = match(l:txt, @/)

        while l:start >= 0
            let l:end = matchend(l:txt, @/, l:start)

            if l:end > l:start
                call add(l:matches, strpart(l:txt, l:start, l:end - l:start))
            else
                " This is used to handle searches with a pattern like `.\{-}\ze,`
                " which matches characters (as few as possible) up to,
                " but not including, a comma.
                " It's necessary to increment `l:end` to get past the comma.
                let l:end += 1
            endif

            if @/[0] == '^'
                break " To avoid false matches.
            endif

            let l:start = match(l:txt, @/, l:end)
        endwhile
    endfor

    if len(l:matches) > 0
        let l:line_separator = ( &fileformat == 'dos' ) ? "\r\n" : "\n"
        let @+ = join(l:matches, l:line_separator) .. l:line_separator
        echo len(l:matches) .. " matches copied to clipboard."
    else
        echohl ErrorMsg
        echo "No matches found."
        echohl None
    endif
endfunction

" Paste
" =====

" In Normal mode it is not possible to place cursor after the last character, and
" that is possible in Insert mode.
function <SID>PasteInNormalMode() abort
    let l:cursor_column = col(".")
    let l:last_column_of_a_row = col("$") - 1
    if l:cursor_column >= l:last_column_of_a_row
        execute 'normal "+p'
    else
        execute 'normal "+P'
    endif
endfunction

function <SID>PasteInInsertMode() abort
    let l:cursor_column = col(".")
    let l:end_of_the_cursor_line = col("$")
    if l:cursor_column == l:end_of_the_cursor_line
        " `<C-O>` exits Insert mode and executes one command in Normal mode before
        " returning to Insert mode (`:h i_CTRL-O`).
        return "\<C-O>\"+p"
    else
        return "\<C-O>\"+P"
    endif
endfunction

" Netrw
" =====

function <SID>SearchNetrw(fname) abort
    if ! search('\V\^' .. a:fname .. '\$')
        call search('^' .. substitute(a:fname, '\w\zs.*', '', '') .. '.*\/\@<!$')
    endif
endfunction

" Fuzzy find with `fzf` command
" =============================

let g:custom_fzf_command_options = '--expect=ctrl-t,ctrl-x,ctrl-v --reverse --multi'

function <SID>FZF() abort
    execute 'FZF ' .. g:custom_fzf_command_options
endfunction

function <SID>Buffers() abort
    call fzf#run(fzf#wrap({
        \ 'source': reverse(<SID>BufferList()),
        \ 'sink': function('<SID>BufferOpen'),
        \ 'options': '--reverse',
        \ 'down': len(<SID>BufferList()) + 2
    \ }))
endfunction

function! s:BufferList() abort
    redir => l:buffer_list
        silent buffers
    redir END

    let l:buffer_list_extended = []
    let l:buffer_list = split(l:buffer_list, '\n')
    for line in l:buffer_list
        let l:buffer_number = str2nr(matchstr(line, '^[ 0-9]*'))
        let l:buffer_first_line = getbufoneline(bufnr(l:buffer_number), 1)
        let l:buffer_list_extended += [line .. '   ' .. l:buffer_first_line]
    endfor

    return l:buffer_list_extended
endfunction

function! s:BufferOpen(selected_buffer_line) abort
    execute 'buffer' matchstr(a:selected_buffer_line, '^[ 0-9]*')
endfunction

function <SID>RG(args) abort
    let l:searchcommand = 'rg --vimgrep --null --hidden --smart-case --glob "!.git/" --glob "!.tags" -- '
    let l:pattern = '.'
    if len(a:args) > 0
        let l:pattern = a:args
    endif
    call fzf#run(fzf#wrap({
        \ 'source': l:searchcommand .. fzf#shellescape(l:pattern),
        \ 'sinklist': function('s:RgHandler'),
        \ 'options': g:custom_fzf_command_options
    \ }))
endfunction

function! s:RgReturnSplit(line) abort
    " `rg` command returns:
    " <file_name>\x0<line_number>:<column>:<context>
    let l:parts = split(a:line, '[\x0]')
    let l:filename = l:parts[0]
    let l:parts = split(l:parts[1], ':')

    return {
        \ 'filename': l:filename,
        \ 'lnum': l:parts[0],
        \ 'col': l:parts[1],
        \ 'text': join(l:parts[2:], ':')
    \ }
endfunction

function! s:RgHandler(lines) abort
    " First line is reserved for command e.g. `ctrl-t`.
    if len(a:lines) < 2 | return | endif

    let cmd = get({
        \ 'ctrl-x': 'split',
        \ 'ctrl-v': 'vertical split',
        \ 'ctrl-t': '$tabe'
    \ }, a:lines[0], 'edit')
    let list = map(a:lines[1:], 's:RgReturnSplit(v:val)')

    for item in list
        execute cmd escape(item.filename, ' %#\')
        execute item.lnum
        execute 'normal!' .. item.col .. '|zz'
    endfor
endfunction

" Find and replace
" ================

" Find and replace in files
" -------------------------

if !exists('s:find_and_replace_in_files_last_find')
    let s:find_and_replace_in_files_last_find = ''
endif
if !exists('s:find_and_replace_in_files_last_replace')
    let s:find_and_replace_in_files_last_replace = ''
endif
if !exists('find_and_replace_in_files_last_where')
    let s:find_and_replace_in_files_last_where = "--glob '*'"
endif

function <SID>FindAndReplaceInFiles() abort
    if !empty(filter(
        \ range(1, bufnr('$')),
        \ 'bufname(v:val) != "" && getbufvar(v:val, "&modified")'
            \ .. ' && getbufvar(v:val, "&buftype") == ""'
    \ ))
        echohl ErrorMsg
        echom 'Save all file-backed buffers before doing find and replace in files.'
        echohl None
        return
    endif

    let l:find = input(
        \ 'In "' .. getcwd() .. '" find string to replace: ',
        \ s:find_and_replace_in_files_last_find
    \ )
    redraw " Clears the command line.
    if l:find == '' | echo '' | return | endif
    let s:find_and_replace_in_files_last_find = l:find

    let l:replace = input(
        \ 'Replace "' .. l:find .. '" with: ',
        \ s:find_and_replace_in_files_last_replace
    \ )
    let s:find_and_replace_in_files_last_replace = l:replace

    let l:where = input(
        \ 'In "' .. getcwd() .. '" where to replace: ',
        \ s:find_and_replace_in_files_last_where
    \ )
    redraw " Clears the command line.
    if l:where == '' | echo '' | return | endif
    let s:find_and_replace_in_files_last_where = l:where

    " Populate quickfix list with grep results.
    execute 'silent grep! ' .. l:find .. ' ' .. l:where

    " Check if there are valid results in quickfix list.
    if empty(filter(getqflist(), {_, v -> v.bufnr > 0}))
        call setqflist([])
    endif
    call s:DoReplacementFromQuickfixEntries(l:find, l:replace)
endfunction

function <SID>DoReplacementFromQuickfixEntries(find, replace) abort
    " Close the quickfix window.
    execute 'cclose'

    if empty(getqflist()) | echo 'No results found.' | return | endif

    let l:confirm = input('Confirm replacements (yes | no): ', 'yes')
    redraw " Clears the command line.
    if (l:confirm != 'yes' && l:confirm != 'no')
        echohl ErrorMsg
        echo '"' .. l:confirm .. '" is not valid input as replacement confirmation.'
        echohl None
        return
    endif

    " Open the quickfix window.
    execute 'cwindow'

    " If replacement confirmation is used,
    " with `Ctrl-C` interrupt all subsequent confirmation prompts.
    try
        " Do the replacement from all quickfix entries.
        execute 'cdo s/' .. a:find .. '/' .. a:replace .. '/'
            \ .. ( l:confirm == 'yes' ? 'c' : '' )

        " Open every file referenced in the quickfix list in its own tab.
        execute 'set switchbuf+=usetab,newtab'
        execute 'silent cfdo tab sbuffer'
        execute 'set switchbuf-=usetab,newtab'
    catch /^Vim:Interrupt$/
        echohl ErrorMsg
        echo 'Replacement is interrupted.'
        echohl None
    endtry
endfunction

" Find and replace in buffers
" ---------------------------

if !exists('s:find_and_replace_in_buffers_last_find')
    let s:find_and_replace_in_buffers_last_find = ''
endif
if !exists('s:find_and_replace_in_buffers_last_replace')
    let s:find_and_replace_in_buffers_last_replace = ''
endif

function <SID>FindAndReplaceInBuffers() abort
    let l:find = input(
        \ 'In open buffers find string to replace: ',
        \ s:find_and_replace_in_buffers_last_find
    \ )
    redraw " Clears the command line.
    if l:find == '' | echo '' | return | endif
    let s:find_and_replace_in_buffers_last_find = l:find

    let l:replace = input(
        \ 'Replace "' .. l:find .. '" with: ',
        \ s:find_and_replace_in_buffers_last_replace
    \ )
    redraw " Clears the command line.
    let s:find_and_replace_in_buffers_last_replace = l:replace

    " Clear quickfix list.
    call setqflist([])

    " `:grep` can only be used on files.
    " For buffers we use custom function to populate quickfix list,
    " similar to how `:grep` does it.
    bufdo call <SID>AddQuickfixListEntriesFromBuffer(l:find)

    call <SID>DoReplacementFromQuickfixEntries(l:find, l:replace)
endfunction

function! s:AddQuickfixListEntriesFromBuffer(pattern) abort
    " Skip if the buffer is the special one, e.g., `help`, `quickfix`, etc.
    if (&buftype != '') | return | endif

    let l:lines = getbufline(bufnr('%'), 1, '$')
    let l:lnum = 1

    for l:line in l:lines
        let l:start = 0

        while v:true
            silent! let l:m = matchstrpos(l:line, a:pattern, l:start)

            " `m[0]` is the matched string returned by `matchstrpos()`.
            " `m[1]` is the start column.
            " `m[2]` is the end column.
            if l:m[1] == -1
                break
            endif

            call setqflist([{
                \ 'bufnr': bufnr('%'),
                \ 'lnum': l:lnum,
                \ 'col': l:m[1] + 1,
                \ 'text': l:line
            \ }], 'a')

            " `l:start + 1` is needed to avoid infinite loop if match is zero-length,
            " e.g., a pattern like `\zs` or `\ze`.
            let l:start = (l:m[2] > l:start ? l:m[2] : l:start + 1)
        endwhile

        let l:lnum += 1
    endfor
endfunction

" ============
" Autocommands
" ============

if has("autocmd")
    augroup default
        " Remove all autocommands for the current group.
        autocmd!

        if has('nvim')
            " Start terminal in Insert mode.
            autocmd TermOpen,TermEnter * startinsert
        endif

        " Automatically reload configuration when modified.
        autocmd BufWritePost *vimrc,*exrc,*init.vim :call feedkeys(":source %\<CR>")

        " Check if any buffers were changed outside of current Vim instance.
        "
        " If multiple Vim instances edit the same file, which is changed and saved
        " in one of them. Vim will auto reload file in another instance,
        " if all of its changes are saved.
        " If there are unsaved changes in another instance, Vim will prompt us what to do.
        autocmd FocusGained,BufEnter * :checktime

        " Automatically decide what to do when a swap file exists.
        "
        " Here we want to avoid a prompt when file is opened in another Vim instance.
        "
        " For a single file Vim creates separate swap file for each Vim instance
        " in which it's opened. If crash happens and file recovery is necessary
        " Vim will offer all of the swap files to be used for recovery.
        let g:swap_file_message = ''
        autocmd SwapExists * call <SID>SwapFileHandle()
        autocmd VimEnter * call timer_start(37, {-> <SID>SwapFileMessage()})

        " Automatically recover if the file has a `.sw*` extension,
        " which is used for swap files.
        " We can enter swap file directory and recover buffers which weren't backed by a file
        " when crash happened.
        autocmd BufRead *.sw* execute 'recover'

        " Strip trailing white space on save for all files.
        "
        " For only some file types use:
        " autocmd FileType c,cpp,java,php,ruby,python autocmd BufWritePre <buffer>
        "     \ :call <SID>StripTrailingWhitespaces()
        autocmd BufWritePre * call <SID>StripTrailingWhitespaces()
    augroup END

    augroup netrw
        autocmd!

        " Focus previous active file in Netrw buffer when switching to it with `:Explore`.
        " We redefine `:Explore` command, and use `expand('%:t')` to get name
        " of current file.
        " `autocmd VimEnter` is needed, because the Netrw plugin is sourced
        " after `vimrc` has been loaded and otherwise this command would be
        " overwritten using the default definition.
        autocmd VimEnter * com! -nargs=* -bar -bang -count=0 -complete=dir Explore
            \ execute "call netrw#Explore(<count>,0,0+<bang>0,<q-args>)"
            \ .. ' | call <SID>SearchNetrw(' .. string(expand('%:t')) .. ')'
    augroup END

    augroup netrw_mapping
        autocmd!

        " Go up a directory.
        autocmd FileType netrw nmap <buffer> h -
        autocmd FileType netrw nmap <buffer> <left> -

        " Open directory or a file.
        autocmd FileType netrw nmap <buffer> l <CR>
        autocmd FileType netrw nmap <buffer> <right> <CR>

        " Go back in history.
        autocmd FileType netrw nmap <buffer> H u

        " Toggle the dotfiles.
        autocmd FileType netrw nmap <buffer> . gh

        " Close the preview window which was opened with `p`.
        autocmd FileType netrw nmap <buffer> P <C-W>z

        " Create a file.
        autocmd FileType netrw nmap <buffer> fc %:w<CR>

        " Rename a file.
        autocmd FileType netrw nmap <buffer> fr R

        " Delete a file.
        autocmd FileType netrw nmap <buffer> fd D
    augroup END

    " Overrides for desert colorscheme.
    augroup desert_override
        autocmd!

        " Configure cursor line.
        autocmd ColorScheme desert
            \ highlight CursorLine term=NONE cterm=NONE gui=NONE ctermbg=08
        " Configure line number of the current line.
        autocmd ColorScheme desert
            \ highlight CursorLineNr term=bold cterm=bold gui=bold ctermbg=89 guibg=#87005f

        " Configure normal text (default background color).
        autocmd ColorScheme desert
            \ highlight Normal term=NONE cterm=NONE gui=NONE
            \ ctermfg=15 ctermbg=236 guifg=#FFFFFF guibg=#303030

        " Configure white spaces.
        autocmd ColorScheme desert
            \ highlight Whitespace term=NONE cterm=NONE gui=NONE ctermfg=75 guifg=#71716b

        " Tab line.
        autocmd ColorScheme desert
            \ highlight TabLine guifg=#FFFFFF guibg=#000000 gui=NONE
            \ ctermfg=15 ctermbg=0 cterm=NONE
        autocmd ColorScheme desert
            \ highlight TabLineSel guifg=#000000 guibg=#FFFFFF ctermfg=0 ctermbg=15

        " Status line.
        autocmd ColorScheme desert
            \ highlight StatusLine guifg=#000000 guibg=#FFFFFF
            \ cterm=NONE ctermfg=0 ctermbg=15
        autocmd ColorScheme desert
            \ highlight StatusLineNC guifg=#FFFFFF guibg=#000000
            \ cterm=NONE ctermfg=15 ctermbg=0

        " Status line Insert.
        autocmd ColorScheme desert
            \ highlight User1 guifg=#010101 guibg=#FFFF00 cterm=NONE ctermfg=16 ctermbg=11

        " Status line Visual.
        autocmd ColorScheme desert
            \ highlight User2 guifg=#010101 guibg=#00FF00 ctermfg=16 ctermbg=10

        " Status line Replace/Virtual replace.
        autocmd ColorScheme desert
            \ highlight User3 guifg=#010101 guibg=#FF00FF cterm=NONE ctermfg=16 ctermbg=13
    augroup END
endif

" =============
" Configuration
" =============

" Colorscheme
" ===========

set background=dark
if !has('gui_running') && &term =~ '\%(screen\|tmux\)'
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum" " Set foreground color.
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum" " Set background color.
endif

if has('nvim') || (index(getcompletion('', 'color'), 'unokai') < 0)
    colorscheme desert
else
    colorscheme unokai
endif

set t_Co=256 " Enable 256 colors.
set termguicolors " Enable GUI colors for the terminal to get true color.

" GUI
" ===

if has('gui_running')
    " Disable cursor blinking in Normal mode.
    set guicursor+=n:blinkon0
    " Blink cursor in Insert mode to help us notice that we are not in Normal mode.
    set guicursor+=i:ver37-blinkwait750-blinkon250-blinkoff250
endif

if has('gui_running') && !has('nvim')
    if has('gui_gtk3')
        set guifont=Hack\ 16
    elseif has('gui_win32')
        set guifont=Consolas:h16
    elseif has('gui_macvim')
        set guifont=Monaco:h22

        " If Hack font is installed with the command: brew install --cask font-hack
        " It will be used if the following line is uncommented.
        " set guifont=Hack:h22
    endif

    set guioptions-=e " Hide tab line.
    set guioptions-=m " Hide menu.
    set guioptions-=T " Hide toolbar.
    set guioptions-=r " Hide right-hand scrollbar.
    set guioptions-=R " Hide right-hand scrollbar when there is a vertically split window.
    set guioptions-=l " Hide left-hand scrollbar.
    set guioptions-=L " Hide left-hand scrollbar when there is a vertically split window.
    set guioptions-=b " Hide bottom scrollbar.
    " Keep the GUI window size when adding/removing a scrollbar, or toolbar, tabline, etc..
    set guioptions+=k

    " Set default window size and position.
    set lines=52
    set columns=98
    winpos 0 0
endif

" Commands
" ========

" Enable file type detection.
" Enable loading the plugin files for specific file types.
" Enable loading the indent file for specific file types.
filetype plugin indent on

syntax on " Switch syntax highlighting on.

" Definitions
" -----------

" Shows differences between the current buffer and the file it was loaded from,
" i.e. it shows unsaved changes, see `:h :DiffOrig`.
" It opens a new tab and moves it before the origin one.
" Closing the tab with the diff will return focus to the origin tab.
command! DiffOrig tabnew % | -tabmove | aboveleft vnew | set buftype=nofile
    \ | read ++edit # | 0d_ | diffthis | wincmd p | diffthis

" Shows differences between the current buffer and a buffer identified by its buffer ID.
" It opens a new tab and moves it before the origin one.
" Closing the tab with the diff will return focus to the origin tab.
command! -nargs=1 DiffBuffById tabnew | buffer # | -tabmove | belowright vnew
    \ | execute 'buffer ' .. <args> | diffthis | wincmd p | diffthis

" Executes system `ctags` command which recursively generates tags from current directory.
" Sending `stdout` and `stderr` to `/dev/null` prevents output from appearing in the buffer
" and stopping tag generation on warnings.
command! MakeTags silent !ctags -f .tags -R . &>/dev/null &

command! -nargs=* Files call <SID>FZF()

command! -nargs=* Buffers call <SID>Buffers()

" Create scratch buffer.
command! Scratch enew | setlocal bufhidden=wipe buftype=nofile noswapfile

" Call as `:Rg <pattern>`.
command! -nargs=* Rg call <SID>RG(<q-args>)

" Reverse selection.
command! -range=% -nargs=0 -bar Reverse <line1>,<line2>global/^/move <line1>-1 | nohlsearch

" It works by adding random number and space to start of the line, sorting by number,
" and removing what was added.
command! -range=% -nargs=0 -bar Shuffle :silent!
    \ <line1>,<line2>s/^/\=reltimestr(reltime())[-1:] .. " "/
    \ |<line1>,<line2>sort n
    \ |<line1>,<line2>s/^\S* //
    \ |nohlsearch

" Key mappings
" ============

" Leader
" ------

" Namespace for user mappings.
" To define a mapping which uses the `mapleader` variable, the special string
" `<Leader>` can be used. It is replaced with the string value of `mapleader`.
let mapleader="\<Space>"
let maplocalleader=','

" Custom commands
" ---------------

" Show unsaved changes in a new tab and move the tab before the origin one.
" Closing tab with the diff will return focus to the origin tab.
nnoremap <F5> :DiffOrig<CR>

" Diff the current buffer with the buffer ID which is entered on a prompt.
" Diff is opened in a new tab and moved before the origin one.
" Closing tab with the diff will return focus to origin tab.
nnoremap <C-F5> :call <SID>DiffCurrentWithBufferById()<CR>

" Create tags.
nnoremap <Leader>mm :MakeTags<CR>

" Clipboard
" ---------

" Yank all lines containing match of last used search pattern.
nnoremap <Leader>yl :call <SID>YankAllLinesContainingMatchOfLastUsedSearchPattern()<CR>

" Yank matches of last used search pattern.
nnoremap <Leader>ym :call <SID>YankMatchesOfLastUsedSearchPattern()<CR>

" Copy.
if has('gui_macvim')
    nnoremap <C-Help> "+y
    vnoremap <C-Help> "+y
else
    nnoremap <C-Insert> "+y
    vnoremap <C-Insert> "+y
endif

" Paste.
if has('gui_macvim')
    vnoremap <S-Help> "+p
else
    vnoremap <S-Insert> "+p
endif

" It is not possible to paste before last character,
" in Normal mode use `"+p` or `"+P` to do that.
if has('gui_macvim')
    nnoremap <S-Help> :call <SID>PasteInNormalMode()<CR>
else
    nnoremap <S-Insert> :call <SID>PasteInNormalMode()<CR>
endif

" We use `<C-R>` and call the function to get Insert mode cursor position
" without switching to Normal mode, see `:help i_CTRL-R`.
if has('gui_macvim')
    inoremap <S-Help> <C-R>=<SID>PasteInInsertMode()<CR>
else
    inoremap <S-Insert> <C-R>=<SID>PasteInInsertMode()<CR>
endif

" Paste clipboard register to command line.
if has('gui_macvim')
    cnoremap <S-Help> <C-R>+
else
    cnoremap <S-Insert> <C-R>+
endif

" Paste to terminal window like one used by `fzf`.
if has('nvim')
    " `<C-\><C-N>` enters Normal mode in terminal.
    tnoremap <expr> <S-Insert> '<C-\><C-N>pi'
elseif has('gui_macvim')
    tnoremap <S-Help> <C-w>"+
else
    tnoremap <S-Insert> <C-w>"+
endif

" Files
" -----

" Fuzzy find files.
nnoremap <Leader>ff :Files<CR>

" Find file and open in new tab. Hit <Tab> after entering search term.
nnoremap <Leader>fe :tabfind *

" Find file and open in the current tab. Hit <Tab> after entering search term.
nnoremap <Leader>fx :find *

" Find string in files. After entering pattern hit enter.
nnoremap <Leader>fs :Rg<Space>

" Find and replace (alter) string in files.
" Input parameters on prompts. If replacement confirmation is used,
" with `Ctrl-C` interrupt all subsequent confirmation prompts.
nnoremap <Leader>fa :call <SID>FindAndReplaceInFiles()<CR>

" Buffers
" -------

" Find and replace (alter) string in buffers.
" Input parameters on prompts. If replacement confirmation is used,
" with `Ctrl-C` interrupt all subsequent confirmation prompts.
nnoremap <Leader>ba :call <SID>FindAndReplaceInBuffers()<CR>

" Edit a new, unnamed buffer.
nnoremap <S-F4> :enew<CR>

" List buffers which then can be opened with `:b <BUFFER_NUMBER>`.
nnoremap <Leader>bl :buffers<CR>

" Echo current buffer ID.
nnoremap <Leader>bi :echo 'Buffer ID: ' .. bufnr("%")<CR>

" Fuzzy find buffers.
nnoremap <Leader>bf :Buffers<CR>

" Move between buffers.
nnoremap <Leader>bp :bprevious<CR>
nnoremap <C-S-M-h> :bprevious<CR>

nnoremap <Leader>bn :bnext<CR>
nnoremap <C-S-M-l> :bnext<CR>

" Delete all hidden buffers which are saved.
nnoremap <Leader>bxh :call <SID>DeleteHiddenSavedBuffers()<CR>

" Switch to next buffer, unload and delete previous from the buffer list.
nnoremap <expr> <Leader>bd &modified ? ':echoe "Unwritten!!!"<CR>':':bnext<CR>:bdelete#<CR>'
nnoremap <expr> <C-S-F4> &modified ? ':echoe "Unwritten!!!"<CR>':':bnext<CR>:bdelete#<CR>'

" Tabs
" ----

" List tabs which then can be opened with `<TAB_NUMBER>gt`.
nnoremap <Leader>tl :tabs<CR>

" Switch to tab.
nnoremap <M-1> :tabnext 1<CR>
nnoremap <M-2> :tabnext 2<CR>
nnoremap <M-3> :tabnext 3<CR>
nnoremap <M-4> :tabnext 4<CR>
nnoremap <M-5> :tabnext 5<CR>
nnoremap <M-6> :tabnext 6<CR>
nnoremap <M-7> :tabnext 7<CR>
nnoremap <M-8> :tabnext 8<CR>
nnoremap <M-9> :tabnext 9<CR>
nnoremap <M-0> :tabnext 10<CR>

" Open tab, with the current buffer, at the end.
nnoremap <Leader>tt :$tab split<CR>

" Open tab, with the current buffer, next to the current tab.
nnoremap <Leader>tn :tab split<CR>

" Delete tab.
nnoremap <Leader>td :tabclose<CR>
nnoremap <C-F4> :tabclose<CR>

" Move between tabs.
nnoremap <M-h> gT
nnoremap <M-l> gt

" Move tabs.
nnoremap <C-S-PageUp> :-tabmove<CR>
nnoremap <M-S-h> :-tabmove<CR>
nnoremap <C-S-PageDown> :+tabmove<CR>
nnoremap <M-S-l> :+tabmove<CR>

" Tags
" ----

" Jump to tag under cursor.
nnoremap <C-5> <C-]>

" Jump to ambiguous tag under cursor.
nnoremap <Leader>jt g<C-]>

" Jump to ambiguous tag under cursor after splitting window horizontally.
nnoremap <Leader>js <C-w>sg<C-]>

" Jump to ambiguous tag under cursor after splitting window vertically.
nnoremap <Leader>jv <C-w>vg<C-]>

" Panes (windows)
" ---------------

" Change panes (windows).
nnoremap <C-l> <C-w>l
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k

" Resize panes (windows).
nnoremap <C-S-l> 4<C-w><
nnoremap <C-S-h> 4<C-w>>
nnoremap <C-S-j> 4<C-w>-
nnoremap <C-S-k> 4<C-w>+

" Lines
" -----

" Move lines.
" `==` re-indents the line to suit its new position.
nnoremap <C-M-j> :move .+1<CR>==
nnoremap <C-M-k> :move .-2<CR>==
inoremap <C-M-j> <Esc>:move .+1<CR>==gi
inoremap <C-M-k> <Esc>:move .-2<CR>==gi
vnoremap <C-M-j> :move '>+1<CR>gv=gv
vnoremap <C-M-k> :move '<-2<CR>gv=gv

" Move inside of displayed (word wrapped) line.
nnoremap <expr> j (v:count == 0 ? 'gj' : 'j')
nnoremap <expr> k (v:count == 0 ? 'gk' : 'k')
nnoremap <expr> gj (v:count == 0 ? 'j' : 'gj')
nnoremap <expr> gk (v:count == 0 ? 'k' : 'gk')

" Remap arrow keys.
nnoremap <Left> h
nnoremap <expr> <Down> v:count == 0 ? 'gj' : 'j'
nnoremap <expr> <Up> v:count == 0 ? 'gk' : 'k'
nnoremap <Right> l

vnoremap <Left> h
vnoremap <expr> <Down> v:count == 0 ? 'gj' : 'j'
vnoremap <expr> <Up> v:count == 0 ? 'gk' : 'k'
vnoremap <Right> l

" Macros
" ------

" Apply macro which was recorded with `qq`.
nnoremap <F4> @q
nnoremap Q @q

" Apply macro to visual selection.
vnoremap <F4> :normal! @q<CR>

" Search and replace within current buffer
" ----------------------------------------

" Search selection as literal.
" `\V` activates "very nomagic" mode (`:h /\V`),
" but we still need to escape `\` and `/` characters, which are not literals with `\V`.
" `<C-R>=escape(@A,'/\')` inserts escaped contents of the `A` register,
" which was filled with yank `"ay`.
vnoremap <F3> "ay<BS>/\V<C-R>=escape(@A,'/\')<CR><CR>

" Move cursor on top of a word or select some text, press defined combination, type changes,
" exit Insert mode. Now press `.` to repeat changes to the next match, or
" first use `n` to select next match before pressing `.` (comparable to multiple cursors).
" `@/` is the last search pattern register, `:help quote/`.
" `<cword>` captures word under cursor to `@/`.
" `cgn` replaces next search hit, `c` is for change,
" `gn` searches forward for the last used pattern.
nnoremap <M-F2> :let @/='\<'.expand('<cword>').'\>'<CR>cgn
xnoremap <M-F2> "sy:let @/=@s<CR>cgn

" Search and replace selection below cursor, `:help :s`.
" `.,$s` is range setting.
" `I` is for case sensitive, replace it with `i` for case insensitive.
" `<C-R>A` inserts contents of the `A` register which was filled with yank `"ay`.
vnoremap <F2> "ay<BS>:.,$s;<C-R>A;<C-R>A;gcI<Left><Left><Left><Left>

" Search and replace selection in whole file.
" `%s` is range setting.
" `I` is for case sensitive, replace it with `i` for case insensitive.
" `<C-R>A` inserts contents of the `A` register which was filled with yank `"ay`.
vnoremap <S-F2> "ay<BS>:%s;<C-R>A;<C-R>A;gcI<Left><Left><Left><Left>

" Sort
" ----

" Sort selection.
" Because correct ordering by case is important we first use case sensitive sorting.
" `i` is for case insensitive.
" It is recommended not to use `\|` to chain commands in map.
" `<CR>`, without padding with spaces, should be used instead,
" see `:h map_bar` and `:h map_return`.
vnoremap <F9> :sort<CR>:normal gv<CR>:sort i<CR>

" Sort selection and reverse results.
vnoremap <S-F9> :sort<CR>:normal gv<CR>:sort i<CR>
    \:normal gv<CR>:Reverse<CR>

" Shuffle selected lines.
vnoremap <C-F9>s :Shuffle<CR>

" Remove duplicate lines.
" `u` is for unique.
vnoremap <C-F9>u :sort u<CR>

" Numerical sorting.
" `n` sorting is done on the first decimal number in the line.
" One leading '-' is included in the number for signed numbers.
vnoremap <C-F9>n :sort n<CR>

" Matching brace and quote
" ------------------------

" `<C-G> U` preserves undo sequence (`:h i_CTRL-G_U`).
inoremap { {}<C-G>U<Left>
inoremap ( ()<C-G>U<Left>
inoremap [ []<C-G>U<Left>
inoremap < <><C-G>U<Left>
inoremap " ""<C-G>U<Left>
inoremap ` ``<C-G>U<Left>
inoremap * **<C-G>U<Left>
" Mapping for `'` is not defined intentionally because it is often used without a pair.

" Surround visual selection.
vnoremap <Leader>{ <Esc>`<i{<Esc>`><Right>a}<Esc>
vnoremap <Leader>( <Esc>`<i(<Esc>`><Right>a)<Esc>
vnoremap <Leader>[ <Esc>`<i[<Esc>`><Right>a]<Esc>
vnoremap <Leader>< <Esc>`<i<<Esc>`><Right>a><Esc>
vnoremap <Leader>" <Esc>`<i"<Esc>`><Right>a"<Esc>
vnoremap <Leader>' <Esc>`<i'<Esc>`><Right>a'<Esc>
vnoremap <Leader>` <Esc>`<i`<Esc>`><Right>a`<Esc>
vnoremap <Leader>* <Esc>`<i*<Esc>`><Right>a*<Esc>
vnoremap <Leader>, <Esc>`<i,<Esc>`><Right>a,<Esc>

" Delete surround of visual selection. When delete is done at the end of the
" line cursor will not be at the end of previously selected text.
vnoremap <Leader>xx <Esc>`>a<BS><Esc>`<i<Delete><Esc>`>2<Left>

" Replace surround of visual selection.
vnoremap <Leader>r{ <Esc>`<r{<Esc>`>r}<Esc>
vnoremap <Leader>r( <Esc>`<r(<Esc>`>r)<Esc>
vnoremap <Leader>r[ <Esc>`<r[<Esc>`>r]<Esc>
vnoremap <Leader>r< <Esc>`<r<<Esc>`>r><Esc>
vnoremap <Leader>r" <Esc>`<r"<Esc>`>r"<Esc>
vnoremap <Leader>r' <Esc>`<r'<Esc>`>r'<Esc>
vnoremap <Leader>r` <Esc>`<r`<Esc>`>r`<Esc>
vnoremap <Leader>r* <Esc>`<r*<Esc>`>r*<Esc>
vnoremap <Leader>r, <Esc>`<r,<Esc>`>r,<Esc>

" Netrw
" -----

" Open local-directory browser in full window and in the directory of current file.
nnoremap <Leader>dd :Explore<CR>
nnoremap - :Explore<CR>

" Open Netrw sidebar in the directory of the current file.
" `100 - g:netrw_winsize` is used to make Netrw windows created with
" `Lexplore` same size as when created with `v` or `o` Netrw commands.
nnoremap <Leader>de :execute (100 - g:netrw_winsize)."Lexplore %:p:h"<CR>

" Open Netrw sidebar in the current working directory.
nnoremap <Leader>dw :execute (100 - g:netrw_winsize).'Lexplore'<CR>

" Terminal
" --------

" In Vim use `<C-W><S-N>` to switch to Normal mode in terminal window,
" use `i` to return to Insert mode.

if has('nvim')
    " Switch to Normal mode in terminal window, use `i` to return to Insert mode.
    tnoremap <C-W> <C-\><C-N><C-W>

    " Simulates <C-R> (insert the contents of a register) in Terminal mode.
    tnoremap <expr> <C-R> '<C-\><C-N>"'.nr2char(getchar()).'pi'
endif

" Transform current buffer's contents using shell commands
" --------------------------------------------------------

" `<Bar>` is used as pipe `|`, we could have also used `\|`.

" `tr` is used to replace single quote with double quote which is valid for `json`.
" `jq` is used to output pretty formatted `json`.
nmap <Leader>cjq :%!tr "'" '"' <Bar> jq .<CR>

" Encode current buffer's contents as base64 string.
nmap <Leader>cb64e :%!base64<CR>

" Decode current buffer's contents from base64 string.
nmap <Leader>cb64d :%!base64 --decode<CR>

" External applications
" ---------------------

" Krusader.
if has('nvim')
    nmap <Leader>ek :call jobstart("krusader --left '" .. expand("%:p:h") .. "'")<CR>
else
    nmap <Leader>ek :call system("krusader --left '" .. expand("%:p:h") .. "' &")<CR>
endif

" git-dag from Git Cola.
if has('nvim')
    nmap <Leader>egd :call jobstart("git-dag '" .. expand("%:p") .. "'")<CR>
else
    nmap <Leader>egd :call system("git-dag '" .. expand("%:p") .. "' &")<CR>
endif

" git gui blame.
if has('nvim')
    nmap <Leader>egb :call jobstart("git gui blame '" .. expand("%:p") .. "'")<CR>
else
    nmap <Leader>egb :call system("git gui blame '" .. expand("%:p") .. "' &")<CR>
endif

" Misc
" ----

if has('gui_running') && !has('nvim')
    " Toggle GUI menu.
    nnoremap <Leader>mgm :if &guioptions=~'m'<Bar>set guioptions-=m<Bar>
        \ else<Bar>set guioptions+=m<Bar>endif<CR>
endif

" Stop highlighting of items found by search (modify highlighting).
nnoremap <Leader>mh :noh<CR>

" Toggle spell checking (modify spell checking).
nnoremap <Leader>ms :set spell!<CR>

" Modify current buffer's filetype to use, for example, different syntax highlighting.
" Use tab to get complete list of supported values.
nnoremap <Leader>mt :set filetype=

nnoremap <F8> :edit $VIM_CONFIG_PATH<CR>

" Indent.
" When used in Normal mode key combination must be entered two times.
vnoremap < <gv
vnoremap > >gv

" `CTRL-U` in insert mode deletes a lot.
" Use `CTRL-G u` to first break undo (`:h i_CTRL-G_u`), so that you can undo `CTRL-U`.
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>

" Options (listed with `:options` command)
" ========================================

set hlsearch " When there is a previous search pattern, highlight all its matches.

let c_comment_strings=1 " Highlighting strings inside C comments.

" 2. moving around, searching and patterns
" ----------------------------------------

" Many jump commands move the cursor to the first non-blank character of a line.
set nostartofline

" `path` is a list of directory names used for file searching (global or local to buffer),
" `,` is separator,
" `.` is directory relative to current file,
" `**` turns on recursive file search,
" empty string between two `,` searches in current working directory, `:help :pwd`.
set path=.,**,,

set incsearch " Show match for partly typed search command.

set ignorecase " Ignore case when using a search pattern.

set smartcase " Override `ignorecase` when pattern has upper case characters.

" 3. tags
" -------

" `,` is separator,
" `./` searches in the directory of the file rather than current working directory,
" `;` first searches in directory that precedes semicolon, then in every parent directory.
set tags=./.tags;,.tags " List of file names to search for tags.

set showfulltag " When completing tags in Insert mode show more info.

" 4. displaying text
" ------------------

if exists('+smoothscroll')
    set smoothscroll " Scroll路by路screen路line (local to window).
endif

set scrolloff=5 " Number of screen lines to show around the cursor.

set linebreak " Wrap long lines at a character in `:h breakat` (local to window).

set breakindent " Preserve indentation in wrapped text (local to window).

set sidescroll=1 " Minimal number of columns to scroll horizontally.

set sidescrolloff=5 " Minimal number of columns to keep left and right of the cursor.

" Include `lastline` to show the last line even if it doesn't fit,
" include `uhex` to show unprintable characters as a hex number.
set display=lastline,uhex

set fillchars= " Characters to use for the status line, folds and filler lines.

set list " Show `<Tab>` as `^I` and end-of-line as `$` (local to window).

" List of strings used for list mode.
set listchars=tab:>-,trail:-,nbsp:+,multispace:路,precedes:<,extends:>

set number " Show the line number for each line (local to window).

set relativenumber " Show the relative line number for each line (local to window).

set numberwidth=5 " Number of columns to use for the line number (local to window).

" 5. syntax, highlighting and spelling
" ------------------------------------

set cursorline " Highlight the screen line of the cursor (local to window).

set colorcolumn=94 " Columns to highlight (local to window).

" 6. multiple windows
" -------------------

set laststatus=2 " Always show status line.

" Alternate format to be used for a status line.
set statusline=%!SetStatusLine()

set hidden " Don't unload a buffer when no longer shown in a window.

set switchbuf=uselast " Which window to use when jumping to a buffer.

set splitbelow " When on, splitting a window will put the new window below the current one.

" When on, splitting a window will put the new window right of the current one.
set splitright

" 7. multiple tab pages
" ---------------------

set showtabline=2 " Always show tab pages line.

set tabpagemax=50 " Maximum number of tab pages to open for `-p` and `tab all`.

" Alternate format to be used for a tab line.
set tabline=%!SetTabLine()

" 8. terminal
" -----------

" Show info in the window title.
set title titlestring=%t%(\ %M%)%(\ (%{expand(\"%:~:.:h\")})%)%(\ %a%)

" 9. using the mouse
" ------------------

set mouse=a " Enable the use of the mouse.

" What the right mouse button is used for: `extend`, `popup` or `popup_setpos`.
set mousemodel=popup_setpos

" 12. messages and info
" ---------------------

set shortmess=filnxtToOFI " List of flags to make messages shorter.

set helplang=en " List of preferred languages for finding help.

set ruler " Show the cursor position all the time.

set showcmd " Display incomplete commands.

set belloff=all " Do not ring the bell for these reasons.

" 13. selecting text
" ------------------

" When to start Select mode instead of Visual mode: `mouse`, `key` and/or `cmd`.
set selectmode=

" Commands such as `:yank` or `:put` normally operate with the unnamed register `""`.
" Set `unnamed` flag to use the `*` register like unnamed register, then yank,
" delete, change and put operations which would normally go to the unnamed
" register will use the clipboard register `"*` instead, which is the `PRIMARY` buffer in X.
" `unnamedplus` is variant of the `unnamed` flag which uses the clipboard register `+`.
" The `"+` clipboard register corresponds to the `CLIPBOARD` buffer in X.
" If `unnamedplus,unnamed` is used then yank operations will also copy the
" yanked text to `"*` register in addition to the `"+` register (however,
" delete, change and put operation will still only operate on the `"+` register)
set clipboard^=unnamedplus,unnamed

" What special keys can do: `startsel` and/or `stopsel`.
set keymodel=startsel,stopsel

" 14. editing text
" ----------------

set backspace=indent,eol,start " Specifies what `<BS>`, `CTRL-W`, etc. can do in Insert mode.

set formatoptions+=jcroql " List of flags that tell how automatic formatting works.

set nojoinspaces " Use two spaces after `.` when joining a line.

set nrformats=bin,hex " Number formats recognized for `CTRL-A` and `CTRL-X` commands.

" 15. tabs and indenting
" ----------------------

set tabstop=4 " Number of spaces a `<Tab>` in the text stands for (local to buffer).

set shiftwidth=4 " Number of spaces used for each step of (auto)indent (local to buffer).

set smarttab " A `<Tab>` in an indent inserts `shiftwidth` spaces.

set softtabstop=4 " If non-zero, number of spaces to insert for a `<Tab>` (local to buffer).

set expandtab " Expand `<Tab>` to spaces in Insert mode (local to buffer).

set autoindent " Automatically set the indent of a new line (local to buffer).

set nocindent " Disable automatic C program indenting.

" Round to `shiftwidth` for `<<` and `>>`. When at 3 spaces, and `>` is hit go to 4, not 7.
set shiftround

" 18. mapping
" -----------

set timeout " Allow timing out halfway into a mapping.

" Time in msec for `timeout` into a mapping, default timeout is 1000ms, see `:h timeoutlen`.
let s:default_timeoutlen=13337
execute "set timeoutlen=" .. s:default_timeoutlen

set ttimeout " Allow timing out halfway into a key code.

set ttimeoutlen=50 " Time in msec for `timeout` into a key code.

" 19. reading and writing files
" -----------------------------

" Automatically read a file when it was modified outside of Vim (global or local to buffer).
set autoread

set nofsync " Forcibly sync the file to disk after writing it.

" 20. the swap file
" -----------------

if !has('nvim')
    " List of directories for the swap file.
    " If a directory ends in two path separators (e.g. `//`), the swap file
    " name will be built from the complete path to the file with all path
    " separators substituted to `%` signs. This will ensure file name uniqueness.
    set directory=~/.vim/swapfiles//
endif

" 21. command line editing
" ------------------------

set history=10000 " How many command lines are remembered.

set wildmenu " Command-line completion shows a list of matches.

set wildmode=full " Specifies how command line completion works.

set wildignorecase " Ignore case when completing file names.

if !has('nvim')
    " A list of words that change how command line completion is done.
    set wildoptions=pum,tagfile,fuzzy
endif

" 23. running make and jumping to errors (quickfix)
" -------------------------------------------------

" Configure the `:grep` command.
if executable('rg')
    set grepprg=rg\ --vimgrep\ --hidden\ --smart-case\ --glob\ \"!.git/\"\ --glob\ \"!.tags\"
endif

" 25. multi-byte characters
" -------------------------

set encoding=utf-8 " Character encoding used internally.

" 26. various
" -----------

" List of words that specifies what to put in a session file.
set sessionoptions=blank,buffers,curdir,folds,help,tabpages,winsize

" List of words that specifies what to save for `:mkview`.
set viewoptions=folds,cursor,curdir

" Disable viminfo `:h viminfo`.
set viminfofile=NONE

" =======
" Plugins
" =======

" First party plugins (built-in)
" ==============================

runtime ftplugin/man.vim
packadd! matchit
packadd! comment

" Netrw
" -----

" Size of the new windows (in %) when Netrw creates a split with `v` or `o`.
let g:netrw_winsize = 75

let g:netrw_list_hide = '\(^\|\s\s\)\zs\.\S\+' " Hide dotfiles.

let g:netrw_liststyle = 0 " Thin listing (one file per line).

let g:netrw_banner = 0 " To show banner temporarily use `I` inside Netrw.

let g:netrw_sort_options='i' " Case insensitive sort.

" Third party plugins
" ===================

" FZF.vim (part of `fzf` binary package), NOT the same as https://github.com/junegunn/fzf.vim
" -------------------------------------------------------------------------------------------

" Documentation: https://github.com/junegunn/fzf/blob/master/README-VIM.md

if filereadable('/usr/share/doc/fzf/examples/plugin/fzf.vim')
    source /usr/share/doc/fzf/examples/plugin/fzf.vim
elseif filereadable('/opt/homebrew/opt/fzf/plugin/fzf.vim')
    source /opt/homebrew/opt/fzf/plugin/fzf.vim
endif

" let g:fzf_layout = { 'left': '~50%' }
